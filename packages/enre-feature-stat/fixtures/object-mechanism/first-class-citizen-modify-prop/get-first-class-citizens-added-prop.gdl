// script

use coref::javascript::*

/**
 * This function only supports query cases whose def and usage are in the same file
 */
fn out(
    filePath: string,
    citizenOid: int,
    // "Function" / "Class"
    citizenType: string,
    citizenStartLine: int,
    citizenStartColumn: int,
    callsiteStartLine: int,
    callsiteStartColumn: int,
    leftNodeType: string,
    rightNodeType: string,
) -> bool {
    let (db = getDefaultDB()) {
        for (be in BinaryExpression(db)) {
            if (filePath = be.getRelativePath() &&
                callsiteStartLine = be.getStartLineNumber() &&
                callsiteStartColumn = be.getStartColumnNumber() &&
                isEqualsToken(be.getOperator().to<Node>()) &&
                leftNodeType = getNodeTypeString(be.getLeft().to<Node>()) &&
                rightNodeType = getNodeTypeString(be.getRight().to<Node>())
            ) {
                let (citizenName = getLeftMostIdentifierFromAccessExpression(be.getLeft().to<Node>())) {
                    for (func in FunctionLikeDeclaration(db)) {
                        if (func.getFile() = be.getFile() &&
                            func.getName() = citizenName.getName() &&
                            citizenType = "Function" &&
                            citizenOid = func.id &&
                            citizenStartLine = func.getStartLineNumber() &&
                            citizenStartColumn = func.getStartColumnNumber()
                        ) {
                            return true
                        }
                    }

                    for (clz in ClassDeclaration(db)) {
                        if (clz.getFile() = be.getFile() &&
                            forceGetName(clz.to<Node>()) = citizenName.getName() &&
                            citizenType = "Class" &&
                            citizenOid = clz.id &&
                            citizenStartLine = clz.getStartLineNumber() &&
                            citizenStartColumn = clz.getStartColumnNumber()
                        ) {
                            return true
                        }
                    }
                }
            }
        }
    }
}

fn main() {
    output(out())
}
